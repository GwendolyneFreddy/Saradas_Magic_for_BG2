BACKUP ~saradas_magic_2/backup~

//AUTHOR ~SARADAS saradas85@gmail.com~
SUPPORT ~SHS Staff at http://www.shsforums.net/forum/633-bgee-modding~

VERSION ~v2.0.0~

README ~saradas_magic_2/readme/saradas_magic_2-readme-english.html~

ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET bg2_chapter BEGIN // Always block must run only once per install!

		/* ----------------------------------------- *
		 *    Determines which game is installed.    *
		 * ----------------------------------------- */

		OUTER_SET is_tob   = (GAME_IS ~bgt tob tutu tutu_totsc~) ? 1 : 0     // based on vanilla ToB engine
		OUTER_SET is_bg2ee = (GAME_IS "bg2ee") ? 1 : 0                       // b2gee
		OUTER_SET is_eet   = (GAME_IS "eet") ? 1 : 0                         // eet
		OUTER_SET is_ee    = (GAME_IS ~bg2ee eet~) ? 1 : 0                   // EE game

		/* ------------------------ *
		 *    Detects other mods    *
		 * ------------------------ */

		OUTER_SET is_1pp_staff  = ((MOD_IS_INSTALLED ~1pp.tp2~ ~207~) AND (MOD_IS_INSTALLED ~1pp.tp2~ ~400~)) ? 1 : 0  // 1ppv4: Wizards' Staves (core)
		OUTER_SET is_1pp_staff0 = ((MOD_IS_INSTALLED ~1pp.tp2~ ~204~) AND (is_1pp_staff = 0)) ? 1 : 0                  // 1ppv4: Colourable Quarterstaves without 1ppv4: Wizards' Staves (core)
		OUTER_SET is_tobex      = (FILE_EXISTS ~tobex_ini/tobexcore.ini~) ? 1 : 0                                      // Detects ToBEx

		/* ------------------------------------------------------- *
		 *    Sets default variables if they're not set already    *
		 * ------------------------------------------------------- */

		// Variables used to properly install specific EE games ressources and ...
		ACTION_IF (is_ee) BEGIN
			OUTER_SPRINT ee_file "_ee"
		END ELSE BEGIN
			OUTER_SPRINT ee_file ""
		END

		OUTER_PATCH 0 BEGIN
			// Solved the wrong `WIZARD_IMPROVED_ALACRITY` entry in classic spell.ids (misspelled `WIZARD_IMPROVED_ALUCRITY`) that was crashing installation in a way that does not break other mods compatibility.
			LOOKUP_IDS_SYMBOL_OF_INT improved_alacrity ~spell~ 2921
		END

		// Determines if item_use.2da file is installed natively (EE games) or by ToBEx.
		OUTER_SET is_item_use = 0
		ACTION_IF FILE_EXISTS_IN_GAME ~item_use.2da~ BEGIN
			OUTER_SET is_item_use = 1
		END

		// Solved missing `DRAGON_DISCIPLE` entry in classic kit.ids that was crashing installation in a way that does not break other mods compatibility (in case they restore this kit).
		OUTER_SET is_dragon_disciple_kit = IDS_OF_SYMBOL (~kit~ ~DRAGON_DISCIPLE~) // 0x4023 DRAGON_DISCIPLE
		ACTION_IF (is_dragon_disciple_kit > "-1") BEGIN
			OUTER_SPRINT no_dragon_disciple_kit    "!Kit(Protagonist,DRAGON_DISCIPLE)"
			OUTER_SPRINT dragon_disciple_kit_trans "IF ~Kit(Protagonist,DRAGON_DISCIPLE)~ THEN REPLY @132 GOTO GiveDragonSpell"
		END ELSE BEGIN
			OUTER_SPRINT no_dragon_disciple_kit    ""
			OUTER_SPRINT dragon_disciple_kit_trans ""
		END
/*		PRINT "CONTROL DEBUGGING VARIABLES
improved_alacrity variable = %improved_alacrity%
is_dragon_disciple = %is_dragon_disciple% ==> no_dragon_disciple_kit = %no_dragon_disciple_kit%
dragon_disciple_kit_trans = %dragon_disciple_kit_trans%"*/

		/* ------------------------------------------- *
		 *    EET chapters continuity compatibility    *
		 * ------------------------------------------- */
		ACTION_IF is_eet BEGIN
			OUTER_SET bg2_chapter = 12
		END ELSE BEGIN
			OUTER_SET bg2_chapter = 0
		END
		OUTER_FOR (i = 1 ; i <= 10 ; ++i) BEGIN
			OUTER_SET bg2_chapter = bg2_chapter + 1
			OUTER_SPRINT name_source ~bg2_chapter_%i%~
			OUTER_SET EVAL ~%name_source%~ = bg2_chapter
		END

		/* --------------------------------------------------------------------------------------------- *
		 *    Convert strings to UTF-8 for BGEE/BG2EE                                                    *
		 *    No exception, everything needs to be converted                                             *
		 *    setup.tra contains in game texts as well as installation texts and needs to be converted   *
		 * --------------------------------------------------------------------------------------------- */
		ACTION_DEFINE_ARRAY sar#noconvert BEGIN END
		// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
		ACTION_DEFINE_ARRAY sar#reload BEGIN setup END

		LAF HANDLE_CHARSETS
			INT_VAR
				infer_charsets = 1
			STR_VAR
				tra_path = EVAL ~%MOD_FOLDER%/lang~
				noconvert_array = sar#noconvert
				reload_array = sar#reload
		END

	END

END


AUTO_TRA ~saradas_magic_2/lang/%s~

// Languages
LANGUAGE ~English~
         ~english~
         ~saradas_magic_2/lang/english/setup.tra~


/* ====================================================== *
 *    Saradas Magic NPC Mod for BGII  (main component)    *
 * ====================================================== */

BEGIN @0                                          // ~Saradas has a monk sprite (original)~
DESIGNATED 0 LABEL ~saradas_magic_npc_for_bg2_monk~
SUBCOMPONENT @2                                   // ~Saradas Magic NPC Mod for BGII~
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @10 // ~You must have ToB, BGT, BG2:EE or EET installed to install the Saradas Magic mod.~

OUTER_SET mage_appearance = 0

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


BEGIN @1                                          // ~Saradas has a mage sprite (optional)~
DESIGNATED 1 LABEL "saradas_magic_npc_for_bg2_mage"
SUBCOMPONENT @2
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @10

OUTER_SET mage_appearance = 1

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~
